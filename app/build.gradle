apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "app.ogasimli.remoter"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode rootProject.versionCode
        versionName "$rootProject.versionName"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            if (project.hasProperty("AndroidProject.signing")
                    && new File(project.property("AndroidProject.signing").toString()).exists()) {
                Properties props = new Properties()
                def propFile = new File(project.property("AndroidProject.signing").toString())
                if (propFile.canRead()) {
                    props.load(new FileInputStream(propFile))
                    if (props != null && props.containsKey('STORE_FILE') &&
                            props.containsKey('KEY_ALIAS') &&
                            props.containsKey('STORE_PASSWORD') &&
                            props.containsKey('KEY_PASSWORD')) {
                        storeFile = file(props['STORE_FILE'])
                        keyAlias = props['KEY_ALIAS']
                        storePassword = props['STORE_PASSWORD']
                        keyPassword = props['KEY_PASSWORD']
                    } else {
                        println 'keystore.properties found but some entries are missing'
                        signingConfig = null
                    }
                } else {
                    println 'keystore.properties file not found'
                    signingConfig = null
                }
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix = '-d'
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

androidExtensions {
    experimental = true
}

ext {
    junitVersion = '4.12'
    runnerVersion = '1.0.1'
    rulesVersion = '1.0.1'
    espressoVersion = '3.0.2'
    ankoVersion = '0.10.3'
    supportLibVersion = '27.1.1'
    daggerVersion = '2.13'
    roomVersion = '1.1.1'
    retrofitVersion = '2.3.0'
    glideVersion = '4.7.1'
    okhttpVersion = '3.9.1'
    leakCanaryVersion = '1.5.4'
}

dependencies {

    // Local libraries
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rulesVersion"

    // Espresso UI Testing dependencies.
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"

    // Android-Debug-Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // Kotlin Anko
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"
    implementation "org.jetbrains.anko:anko-support-v4-commons:$ankoVersion"

    // Android Support
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // Dagger 2
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Room ORM
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"

    // RxJava/RxAndroid
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    implementation("com.github.bumptech.glide:recyclerview-integration:$glideVersion") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // GSON
    implementation 'com.google.code.gson:gson:2.8.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}

// Ensure the no-op dependency is always used in JVM tests.
configurations.all { config ->
    if (config.name.contains('UnitTest') || config.name.contains('AndroidTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary'
                    && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op',
                        version: details.requested.version)
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
